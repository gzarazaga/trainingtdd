buildscript {
	ext {
		springBootVersion = '1.4.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
	baseName = 'trainingtdd'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: spring_boot_version
	compile('info.cukes:cucumber-java:1.2.4')
	compile('junit:junit:4.12')
	compile('info.cukes:cucumber-junit:1.2.4')
	compile('info.cukes:cucumber-spring:1.2.4')

	testCompile('org.springframework.boot:spring-boot-starter-test')
}

sourceSets {
	acceptanceTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/acceptanceTest/java')
		}
		resources.srcDir file('src/acceptanceTest/resources')
	}
}

//Ensure that the integrationTestCompile/integrationTestRuntime configuration contains the dependencies that are required to compile/run our unit tests.
configurations {
	acceptanceTestCompile.extendsFrom testCompile
	acceptanceTestRuntime.extendsFrom testRuntime
}

task acceptanceTest(type: Test) {
	testClassesDir = sourceSets.acceptanceTest.output.classesDir
	classpath = sourceSets.acceptanceTest.runtimeClasspath

	// Gradle skips tasks whose input and output are up to date.
	// To ensure that your acceptance tests are run every time,
	// tell Gradle that the outputs of the acceptance task should always be considered out of date.
	outputs.upToDateWhen { false }
}

// Ensure that our acceptance tests are run before the check task and that the check task fails the build if there are failing acceptance tests.
// Ensure that our unit tests are run before our acceptance tests. This guarantees that our unit tests are run even if our acceptance tests fails.
check.dependsOn acceptanceTest
acceptanceTest.mustRunAfter test

// Ensure that the HTML reports of unit and acceptance tests are created to different report
// build/reports/acceptanceTest directory contains the HTML report that contains the test results of our acceptance tests.
tasks.withType(Test) {
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

def cucumberVersion = "1.2.4"

dependencies {
	acceptanceTestCompile(
			'info.cukes:cucumber-core:' + cucumberVersion,
			'info.cukes:cucumber-java:' + cucumberVersion,
			'info.cukes:cucumber-java:' + cucumberVersion,
			'info.cukes:cucumber-junit:' + cucumberVersion,
			'info.cukes:cucumber-spring:' + cucumberVersion,
			'org.springframework:spring-beans:4.2.5.RELEASE'
	)
	acceptanceTestRuntime(
			'org.springframework:spring-context:4.2.5.RELEASE',
			'org.springframework:spring-test:4.2.5.RELEASE',
			'org.springframework:spring-tx:4.2.5.RELEASE'
	)
}

